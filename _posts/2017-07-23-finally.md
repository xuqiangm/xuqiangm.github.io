---
title: finally解析
date: 2017-07-23
categories: Java
---

在阅读《深入理解Java虚拟机——JVM高级特性与最佳实践》中“类文件结构”一章节时，里面有对finally字节码的解释，我觉得可以很好的对finally的进行解释，现总结如下。

代码：

```java
public class Inc {
	public int inc()
	{
		int x;
		try
		{
			x = 1;
			return x;
		}
		catch (Exception e)
		{
			x = 2;
			return x;
		}
		finally
		{
			x = 3;
		}
	}
}
```

运行*javap -verbose Inc*，结果如下：

```
public class xqm.Inc
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
{
  public xqm.Inc();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 3: 0

  public int inc();
    descriptor: ()I
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=5, args_size=1
         0: iconst_1
         1: istore_1
         2: iload_1
         3: istore_2
         4: iconst_3
         5: istore_1
         6: iload_2
         7: ireturn
         8: astore_2
         9: iconst_2
        10: istore_1
        11: iload_1
        12: istore_3
        13: iconst_3
        14: istore_1
        15: iload_3
        16: ireturn
        17: astore        4
        19: iconst_3
        20: istore_1
        21: aload         4
        23: athrow
      Exception table:
         from    to  target type
             0     4     8   Class java/lang/Exception
             0     4    17   any
             8    13    17   any
            17    19    17   any
}
SourceFile: "Inc.java"
```

图中的结果省略部分数据。*xqm.Inc()*是默认构造函数，主要看*public int inc()*。

```
         0: iconst_1    将int型1推送至栈顶
         1: istore_1	将栈顶int型数值存入第二个本地变量
         2: iload_1		将第二个int型本地变量推送至栈顶
         3: istore_2	将栈顶int型数值存入第三个本地变量
         4: iconst_3	将int型3推送至栈顶
         5: istore_1	将栈顶int型数值存入第二个本地变量
         6: iload_2		将第三个int型本地变量推送至栈顶
         7: ireturn		从当前方法返回int
         8: astore_2	将栈顶引用型数值存入第三个本地变量
         9: iconst_2	将int型2推送至栈顶
        10: istore_1	将栈顶int型数值存入第二个本地变量
        11: iload_1		将第二个int型本地变量推送至栈顶
        12: istore_3	将栈顶int型数值存入第四个本地变量
        13: iconst_3	将int型3推送至栈顶
        14: istore_1	将栈顶int型数值存入第二个本地变量
        15: iload_3		将第四个int型本地变量推送至栈顶
        16: ireturn		将当前方法返回int
        17: astore        4		将栈顶引用型数值存入第五个本地变量
        19: iconst_3	将int型3推送至栈顶
        20: istore_1	将栈顶int型数值存入第二个本地变量
        21: aload         4		将第五个引用类型变量推送至栈顶
        23: athrow		将栈顶的异常抛出
```

**正常运行**

正常情况下运行的指令行数为0~7，结束时的状态如图所示，可见最终返回的结果是1。

![](http://7xrvqe.com1.z0.glb.clouddn.com/finally-normal.svg)

**异常情况1**

按照异常表的第一行的情况，在指令行0~4抛出java/lang/Exception类型的异常时，跳转到第8行。之后正常运行结束的话，状态如下图(图中的E表示异常，下同)。可见最终返回结果是2.

![](http://7xrvqe.com1.z0.glb.clouddn.com/finally-Exception1.svg)

**异常情况2**

按照异常表的第二行的情况，在指令行0~4抛出任何异常（其实已经过滤掉第一行的Exception类型的异常了，即抛出了非Exception类型的异常），则跳转到17行，之后正常运行结束的话，状态如下图。可见最终会把异常抛出。

![](http://7xrvqe.com1.z0.glb.clouddn.com/finally-Exception2.svg)

**异常情况3**

按照异常表的第三行的情况，在指令行8~13发生任何异常情况，则跳转到17行（即，catch语句中发生了异常）。最终结果同异常情况2.

**异常情况4**

按照异常表的第四行的情况，在指令行17~19抛出异常，跳转到17行，最终结果同异常情况2.

### 总结

从以上分析可以看出，finally中的代码实际上是在return或抛出异常之前进行的，需要注意的是，如果finally中对返回项进行了修改，在ruturn时并不会受该修改的影响。原因是修改的项虽然保存在本地变量中，但是并没有将其推到栈顶。

### 参考

《深入理解Java虚拟机——JVM高级特性与最佳实践》